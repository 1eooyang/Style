apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def alias = properties.get('alias')
def _keyPassword = properties.get('keyPassword')
def _storePassword = properties.get('storePassword')
def keystorePath = properties.get('keystorePath')
def buglyAppId = properties.get('buglyAppId')
def flurryApiKey = properties.get('flurryApiKey')

def CHANNEL = System.getProperty("channel", "default")
def APP_NAME = System.getProperty("app_name", "Style")

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION as String
    defaultConfig {
        applicationId "com.yalin.style"
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode APP_VERSION_CODE as int
        versionName APP_VERSION_NAME as String

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "BUGLY_APP_ID", "\"${buglyAppId}\"")
        buildConfigField("String", "FLURRY_API_KEY", "\"${flurryApiKey}\"")

        resValue("string", "app_name_ext", "${APP_NAME}")
    }
    signingConfigs {
        release {
            keyAlias alias
            keyPassword _keyPassword
            storeFile file(keystorePath)
            storePassword _storePassword
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro', 'component-proguard.pro'

            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        demo {
            buildConfigField("boolean", "DEMO_MODE", "true")
        }
        production {
            buildConfigField("boolean", "DEMO_MODE", "false")
        }
        googleprod {
            buildConfigField("boolean", "DEMO_MODE", "false")
        }
    }

}

dependencies {
    compile "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:exifinterface:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:recyclerview-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:percent:${SUPPORT_LIBRARY_VERSION}"

    compile "com.google.code.gson:gson:${GSON_VERSION}"
    compile "com.squareup.okhttp3:okhttp:${OK_HTTP_VERSION}"
    compile "com.google.dagger:dagger:${DAGGER_VERSION}"
    compile "io.reactivex.rxjava2:rxjava:${RXJAVA_VERSION}"
    compile "io.reactivex.rxjava2:rxandroid:${RXANDROID_VERSION}"
    compile "com.github.bumptech.glide:glide:${GLIDE_VERSION}"
    compile "org.greenrobot:eventbus:${EVENT_BUS_VERSION}"
    compile "com.tencent.bugly:crashreport:${BUGLY_VERSION}"
    compile "com.flurry.android:analytics:${FLURRY_VERSION}"

    demoCompile "com.google.firebase:firebase-core:${FIRE_BASE_VERSION}"
    demoCompile "com.google.firebase:firebase-crash:${FIRE_BASE_VERSION}"
    demoCompile "com.google.firebase:firebase-ads:${FIRE_BASE_VERSION}"
    googleprodCompile "com.google.firebase:firebase-core:${FIRE_BASE_VERSION}"
    googleprodCompile "com.google.firebase:firebase-crash:${FIRE_BASE_VERSION}"
    googleprodCompile "com.google.firebase:firebase-ads:${FIRE_BASE_VERSION}"

//    annotationProcessor "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    kapt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

    compile 'com.afollestad.material-dialogs:core:0.9.4.5'

    compile project(':domain')
    compile project(':engine')
    demoCompile project(path: ':data', configuration: 'demoDebug')
    productionCompile project(path: ':data', configuration: 'productionRelease')
    googleprodCompile project(path: ':data', configuration: 'productionRelease')

    demoCompile 'com.facebook.stetho:stetho:1.5.0'

    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

project.afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def oldFile = output.outputFile

            output.outputFile = new File(oldFile.parent,
                    "${APP_NAME}-${CHANNEL}.apk")
        }
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${KOTLIN_VERSION}"
    }
}
apply plugin: 'com.google.gms.google-services'